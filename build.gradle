buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:8.4.2"
        //classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.1'
    }
}

// See http://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html.
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc).tap {
            configureEach {
                options.addStringOption('Xdoclint:none', '-quiet')
            }
        }
    }
}

subprojects { project ->
    repositories {
        google()
        mavenCentral()
    }

    apply plugin: 'checkstyle'

    checkstyle {
        toolVersion = '6.12.1'
    }

    checkstyle {
        configFile = rootProject.file('checkstyle.xml')
        configProperties.checkStyleConfigDir = rootProject.rootDir
    }

    tasks.register('checkstyle', Checkstyle) {
        source 'src'
        include '**/*.java'
        exclude '**/gen/**'
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile).tap {
            configureEach {
                options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
            }
        }
    }
}

ext {
    GLIDE_MAJOR_VERSION = GLIDE_VERSION.split("\\.")[0] as String
    VERSION_NAME = "${DECODER_VERSION}.${GLIDE_VERSION}"
}